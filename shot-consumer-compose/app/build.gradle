plugins {
    id "com.android.application"
    id "kotlin-android"
}
apply plugin: "shot"

android {
    compileSdk 33

    defaultConfig {
        applicationId "com.karumi.shotconsumercompose"
        minSdkVersion 21
        targetSdkVersion 33
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "com.karumi.shot.ShotTestRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
        }
        debug {
            applicationIdSuffix ".debug"
        }
    }
    flavorDimensions 'api'
    productFlavors {
        staging {
            dimension "api"
            applicationIdSuffix ".staging"
            versionNameSuffix "-staging"
        }
        pro {
            dimension "api"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17.toString()
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion "1.4.7"
        kotlinCompilerVersion kotlin_version
    }
    packagingOptions {
        exclude "META-INF/*"
    }
    testOptions {
        if (System.getenv("orchestrated") == "true") {
            execution "ANDROIDX_TEST_ORCHESTRATOR"
        }
    }

    namespace 'com.karumi.shotconsumercompose'
    testNamespace 'com.karumi.shotconsumercompose.test'
}

shot {
    tolerance = 1.1 //1.1%
    showOnlyFailingTestsInReports = false
}

dependencies {

    implementation "androidx.core:core-ktx:1.10.1"
    implementation "androidx.appcompat:appcompat:1.6.1"
    implementation "com.google.android.material:material:1.9.0"
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.material:material:$compose_version"
    implementation "androidx.compose.ui:ui-tooling:$compose_version"
    implementation "androidx.activity:activity-compose:1.7.2"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.6.1"

    testImplementation "junit:junit:4.13.2"

    androidTestUtil "androidx.test:orchestrator:1.4.2"

    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_version"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.5.1"
    androidTestImplementation "androidx.test:core-ktx:1.5.0"
    androidTestImplementation "androidx.test:core:1.5.0"
    androidTestImplementation "androidx.test:runner:1.5.2"
    androidTestImplementation "androidx.test.ext:junit:1.1.5"
    androidTestImplementation "androidx.test.ext:junit-ktx:1.1.5"
}
